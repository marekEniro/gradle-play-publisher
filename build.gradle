buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

apply plugin: 'groovy'
apply from: 'gradle-mvn-push.gradle'

group = GROUP
version = VERSION_NAME

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {

    repositories {
        mavenCentral()
    }

    compile gradleApi()

    compile 'com.android.tools.build:gradle:1.1.3'
    compile('com.google.apis:google-api-services-androidpublisher:v2-rev19-1.20.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }
    compile 'commons-lang:commons-lang:2.6'
    compile 'commons-io:commons-io:1.4'

    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: mavenLocal().url)

                pom.project {
                    developers {
                        developer {
                            id 'bhurling'
                            name 'Bjoern Hurling'
                        }
                        developer {
                            id 'ChristianBecker'
                            name 'Christian Becker'
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies = pom.dependencies.findAll { dep ->
                        dep.scope != 'test'
                    }
                }
            }
        }
    }
}